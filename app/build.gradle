plugins {
    id "co.riiid.gradle" version "0.4.2"
}

apply plugin: 'com.android.application'

if (project.hasProperty("coxswain-private") && new File(project.property("coxswain-private")).exists()) {
    apply from: project.property("coxswain-private")
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "svenmeier.coxswain"
        minSdkVersion 16
        targetSdkVersion 29

        versionCode Integer.parseInt(project.property("coxswain-versionCode"))
        versionName project.property("coxswain-versionName")
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

ext {
    supportLibraryVersion = '1.0.0'
    playServicesVersion = '18.0.0'
}

dependencies {
    implementation files('aars/antpluginlib_3-8-0.aar')
    implementation "androidx.gridlayout:gridlayout:$supportLibraryVersion"
    implementation "androidx.legacy:legacy-support-v13:$supportLibraryVersion"
    implementation "androidx.legacy:legacy-preference-v14:$supportLibraryVersion"
    implementation 'com.google.android.material:material:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0'
    implementation "com.google.android.gms:play-services-fitness:$playServicesVersion"
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    testImplementation 'junit:junit:4.13'
    testImplementation('org.robolectric:robolectric:4.3.1') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    implementation project(':propoid-core')
    implementation project(':propoid-db')
    implementation project(':propoid-ui')
    implementation project(':propoid-util')
}

github {
    owner = 'svenmeier'
    repo = 'coxswain'
    tagName = project.property("coxswain-versionName")
    targetCommitish = 'master'
    name = 'v' + project.property("coxswain-versionName")
    body = new File("changelog").getText("UTF-8")
    assets = [
            'app/build/outputs/apk/release/app-release.apk',
    ]
}
